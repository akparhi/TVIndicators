//@version=3
study(title = "Renko+Moving Average+RMI Alert R3 by JustUncleL", shorttitle="RENKO_MA_RMI", overlay=true)


// Medium Fast MA - type, source, length
ema_len    = input(defval=6, title="MA - Length", minval=1)
ema_src    = input(close, title="MA - Source")
exitfirst = input(false,title="Exit Trade on 1st Opposite Renko Brick")
// inputs for RMI function
rmilen = input(4, title="RMI/RSI MA Length")
mom = input(5, title="Momentum Length",minval=0)
ob = input(70,title="Overbought")
os = input(30,title="Oversold")
uRSI   = input(false,title="Use RSI instead of RMI")
udopen = input(true,title="Use Daily Open Line Filter")
dperiod = input("1D", title="Period for Daily Open Line", options=["1D","1W","1M"])

// Constants
green100 = #008000FF
lime100  = #00FF00FF
red100   = #FF0000FF
blue100  = #0000FFFF
aqua100  = #00FFFFFF
darkred100 = #8B0000FF
gray100  = #808080FF
dodgerblue = #1E90FF

ema_series = ema(close, ema_len)

// Get direction based on MA
direction = 0
direction := rising(ema_series, 3) ? 1 : falling(ema_series, 3) ? -1 : nz(direction[1])

// Plot MA series and color it according too direction
pcol = direction>0 ? lime : direction<0 ? red : na
plot(ema_series, title="MA Plot", color=pcol, linewidth=2,style=line,join=true, transp=10)

//calc rmi
up = ema(max(close - close[mom], 0), rmilen)
dn = ema(max(close[mom] - close, 0), rmilen)
rmi = dn == 0 ? 0 : 100 - 100 / (1 + up / dn)

// Use RSI instead of RMI
rmi := uRSI ? rsi(close, rmilen) : rmi 

// === Calculate Alerts
PA = close > open ? 1 : close < open ? -1 : 0 
HAS = direction
RMI = rmi>=rmi[1] and rmi>=ob ? 1 : rmi<=rmi[1] and rmi<=os ? -1 : 0

long = PA==1 and HAS==1 and RMI==1
short = PA==-1 and HAS==-1 and RMI==-1

olong = 0
olong := olong[1]>0 and (HAS<0 or (not uRSI and RMI<=0) or (exitfirst and (close<open and close[1]>open[1])))? 0 : nz(olong[1])==0 and long? 1 : olong[1]>0 and HAS==1? olong[1]+1 : 0
oshort = 0
oshort := oshort[1]>0 and (HAS>1 or (not uRSI and RMI>=0) or (exitfirst and (close>open and close[1]<open[1])))? 0 : nz(oshort[1])==0 and short? 1 : oshort[1]>0 and HAS==-1? oshort[1]+1 : 0

// - Plot Alerts
plotarrow(olong==1?1:na, title="BUY Arrow", colorup=lime, maxheight=60, minheight=50, transp=20,offset=0)
plotarrow(oshort==1?-1:na, title="SELL Arrow", colordown=red, maxheight=60, minheight=50, transp=20,offset=0)
//
plotshape(olong[1]>0 and olong==0, title='BUY Exit', style=shape.xcross, location=location.belowbar, color=gray, text="Exit\nBuy", offset=0,transp=0)
plotshape(oshort[1]>0 and oshort==0, title='Sell Exit', style=shape.xcross, location=location.abovebar, color=gray, text="Exit\nSell", offset=0,transp=0)

// - Signal Alerts to TV Alarm system
alertcondition(olong==1,title="Swing BUY Alert",message="BUY")
alertcondition(oshort==1,title="Swing SELL Alert",message="SELL")
alertcondition(olong[1]>0 and olong==0,title="Swing BUY Exit Alert",message="BUY Exit")
alertcondition(oshort[1]>0 and oshort==0,title="Swing SELL Exit Alert",message="SELL Exit")

